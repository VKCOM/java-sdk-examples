apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.vk.api'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation('com.vk.api:sdk:1.0.16')

    implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: '2.0.9'

    //Binding for Log4J
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.2'

    //Log4j API and Core implementation required for binding
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

    implementation group: 'org.asynchttpclient', name: 'async-http-client', version: '2.12.3'
}

def releaseBuild = !version.endsWith('-SNAPSHOT')

test {
    useJUnitPlatform()
}

signing {
    required { releaseBuild && gradle.taskGraph.hasTask("publishing") }
    useGpgCmd()
    sign publishing.publications
}

publishing {
    repositories {
        maven {
            def releasesUrl = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsUrl = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            url = !releaseBuild ? snapshotsUrl : releasesUrl
            credentials {
                username = project.properties["ossrhUsername"].toString()
                password = project.properties["ossrhPassword"].toString()
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'vk-java-sdk-examples'
            pom {
                name = 'Examples of usage of Java SDK for VK API'
                packaging = 'jar'
                description = 'Simple project for demonstrating hot to work with Java library for VK API interaction that includes OAuth 2.0 authorization and API methods.'
                url = 'https://github.com/VKCOM/java-sdk-examples'

                scm {
                    url = 'scm:git@github.com:VKCOM/java-sdk-examples.git'
                    connection = 'scm:git@github.com:VKCOM/java-sdk-examples.git'
                    developerConnection = 'scm:git@github.com:VKCOM/java-sdk-examples.git'
                }

                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'ppv-vldmr'
                        name = 'Vladimir Popov'
                        email = 'vv.popov@vk.team'
                    }
                }
            }
        }
    }
}